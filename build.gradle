plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.10' apply false
}

subprojects {
    // mirror repositories to speed up builds
    repositories {
        mavenLocal()
        maven { url 'https://lss233.littleservice.cn/repositories/minecraft/'}
        maven { url 'https://repository.hanbings.io/proxy' }
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public' }
        maven { url "https://maven.aliyun.com/repository/central" }
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://maven.aliyun.com/repository/google" }
        maven { url "https://maven.aliyun.com/repository/jcenter" }
        maven { url "https://maven.aliyun.com/repository/apache-snapshots" }
        maven { url 'https://maven.luna5ama.dev/'}
        maven { url 'https://jitpack.io' }
    }

    apply plugin: 'org.jetbrains.kotlin.jvm'

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = 'UTF-8'
        }
    }

    kotlin {
        jvmToolchain(21)
    }
}

project.ext.mcDir = new File(rootDir, project.ext.mcDir).absolutePath
project.ext.runDir = new File(rootDir, project.ext.runDir).absolutePath
tasks.register('runClient', JavaExec) {
    dependsOn project(':izmk-loader').tasks.merge
    workingDir runDir
    description 'Runs the Minecraft client with IZMK'

    doFirst {
        if (!file(runDir).exists()) {
            file(runDir).mkdirs()
        }
    }

    jvmArgs = [
            "-Xmx4096m",
            "-Dminecraft.client.jar=$mcDir/1.20.6/1.20.6-Forge.jar",
            "-Djava.library.path=$mcDir/natives",
            "-Dlog4j.skipJansi=false",
            "-Dlog4j.configurationFile=$mcDir/log4j.xml",
            "-javaagent:${project(':izmk-loader').tasks.merge.output}=${project(':izmk-loader').projectDir.absolutePath}"
    ]

    classpath = fileTree("$mcDir/libs")
    classpath += fileTree("$mcDir/1.20.6/1.20.6-Forge.jar")

    args = [
            "--username", "xsvf",
            "--version", "IZMK",
            "--gameDir", runDir,
            "--assetsDir", "$mcDir/assets",
            "--assetIndex", "16",
            "--accessToken", UUID.randomUUID().toString(),
            "--launchTarget", "forge_client"
    ]

    mainClass = 'net.minecraftforge.bootstrap.ForgeBootstrap'

    standardOutput = System.out
    errorOutput = System.err
}

tasks.register("runAttach", JavaExec) {
    dependsOn project(':izmk-loader').tasks.merge
    workingDir runDir
    description 'Attaches the Minecraft client with IZMK to a running instance'

    doFirst {
        if (!file(runDir).exists()) {
            file(runDir).mkdirs()
        }
    }

    classpath += fileTree(project(':izmk-loader').tasks.merge.output as File)
    mainClass = "ovo.xsvf.ServiceMain"
}