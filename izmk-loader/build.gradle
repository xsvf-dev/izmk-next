import ovo.xsvf.task.MergeTask

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta8'
    id 'org.jetbrains.kotlin.jvm'
}

group = 'ovo.xsvf'
version = '1.0-SNAPSHOT'

dependencies {
    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    // gson
    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'com.github.LangYa466:EasyLog:1.9'
    implementation 'net.java.dev.jna:jna:5.8.0'
    implementation 'net.java.dev.jna:jna-platform:5.8.0'

    // asm
    implementation 'org.ow2.asm:asm:9.2'
    implementation 'org.ow2.asm:asm-commons:9.2'
    implementation 'org.ow2.asm:asm-tree:9.2'
    implementation 'org.ow2.asm:asm-analysis:9.2'

    implementation project(':izmk-common')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

jar {
    manifest {
        attributes 'Main-Class': 'ovo.xsvf.Launcher'
        attributes 'Premain-Class': 'ovo.xsvf.AgentMain'
        attributes 'Agent-Class' : 'ovo.xsvf.AgentMain'
        attributes 'Can-Redefine-Classes': true, 'Can-Retransform-Classes': true, 'Can-Set-Native-Method-Prefix': true
    }
}

tasks.register('merge', MergeTask) {
    dependsOn project(':izmk-core').tasks.shadowJar
    dependsOn shadowJar

    group 'build'

    loader = tasks.shadowJar.archiveFile.get().asFile
    core = project(':izmk-core').tasks.shadowJar.archiveFile.get().asFile
    output = file("${layout.buildDirectory.asFile.get().absolutePath}/libs/merged-loader.jar")
}

repositories {
    mavenCentral()
}
kotlin {
    jvmToolchain(21)
}