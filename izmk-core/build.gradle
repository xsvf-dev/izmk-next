import ovo.xsvf.task.AccessorTask

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta8'
    id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.jetbrains.kotlin.jvm'
}

group = 'ovo.xsvf'
version = '1.0-SNAPSHOT'

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    reobf = false
    copyIdeResources = true
}

configurations {
    shade
    implementation.extendsFrom shade
}

jar {
    archiveClassifier = 'slim'
}

tasks.register('accessor', AccessorTask) {
    dependsOn shadowJar

    targetFile = tasks.shadowJar.archiveFile.get().asFile
    outputFile = tasks.shadowJar.archiveFile.get().asFile
}

shadowJar {
    configurations = [project.configurations.shade]
    archiveClassifier = ''

    finalizedBy accessor
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    shade project(':izmk-common')
    shade "com.twelvemonkeys.imageio:imageio-core:3.7.0"
    shade 'com.twelvemonkeys.imageio:imageio-metadata:3.7.1'
    shade "com.twelvemonkeys.imageio:imageio-webp:3.7.1"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}
repositories {
    mavenCentral()
}

kotlin {
    jvmToolchain(21)
}